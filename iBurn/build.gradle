buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.jakewharton.hugo'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 5
        versionName "2015.0.1"
    }

    signingConfigs {
        release {
            // We'll read release signingConfig's data from
            // gradle.properties in the project's root directory
        }
        unsigned{
            storePassword = ""
            keyAlias = ""
            keyPassword = ""
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            debuggable false
        }
        unsigned {
            signingConfig signingConfigs.unsigned
            debuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

// Read Signing key from gradle.properties located in project root
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

dependencies {
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    compile 'com.google.android.gms:play-services-maps:7.5.0'
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile project(':libraries:android-gmaps-addons')
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    apt 'net.simonvt.schematic:schematic-compiler:0.6.1'
    compile 'net.simonvt.schematic:schematic:0.6.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
}