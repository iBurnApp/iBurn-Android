buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 2
        versionName "2.0.1.4"
    }

    signingConfigs {
        release {
            // We'll read release signingConfig's data from
            // gradle.properties in the project's root directory
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            debuggable false
            zipAlign true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

// Read Signing key from gradle.properties located in project root
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

dependencies {
    compile 'net.hockeyapp.android:HockeySDK:3.0.2'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:+'
    compile 'com.google.android.gms:play-services:5.0.77'
    compile project(':libraries:android-circlebutton:library')
    compile project(':libraries:android-gmaps-addons')
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:appcompat-v7:19.1.0'
    apt 'net.simonvt.schematic:schematic-compiler:0.5.1'
    compile 'net.simonvt.schematic:schematic:0.5.1'
}

apt {
    arguments {
        schematicOutPackage 'com.gaiagps.iburn.schematic'
    }
}
