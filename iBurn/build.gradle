buildscript {
    repositories {
        mavenCentral()

        dependencies {

            // Add the Crashlytics Gradle plugin
            classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.4'
            classpath "com.google.devtools.ksp:symbol-processing-gradle-plugin:2.1.0-1.0.29"
        }
        google()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-parcelize'

repositories {
    mavenCentral()
}

ext {
    versionYear = 2025
    databaseName = "playaDatabase${versionYear}.1.db"
}

// Generate a Kotlin file containing the current event year for use in code
def generatedSrcDir = "${buildDir}/generated/source/eventyear"
tasks.register('generateEventYear') {
    outputs.dir generatedSrcDir
    doLast {
        def packageDir = new File(generatedSrcDir, 'com/gaiagps/iburn')
        packageDir.mkdirs()
        def outFile = new File(packageDir, 'EventYear.kt')
        outFile.text = """
package com.gaiagps.iburn

object EventYear {
    const val YEAR = ${versionYear}
}
""".trim()
    }
}

kotlin {
    // Use Java 21 for compilation
    jvmToolchain(21)
}


android {
    sourceSets {
        main.java.srcDir(generatedSrcDir)
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
    }
    defaultConfig {
        compileSdk 35
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 61

        versionName "$versionYear.4"

        applicationId "com.iburnapp.iburn3"

        // Provide year-specific resources for use in XML
        resValue "string", "app_name", "iBurn $versionYear"
        resValue "string", "current_year", "$versionYear"
        buildConfigField "String", "DATABASE_NAME", "\"${databaseName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "iburn-$versionName-$versionCode"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    splits {
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86, armeabi-v7a, and mips.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include "x86", "x86_64", "armeabi-v8a", "arm64-v8a", "armeabi-v7a"

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/rxjava.properties', 'META-INF/rxjava.properties']
        }
    }


    signingConfigs {
        release {
            // We'll read release signingConfig's data from
            // gradle.properties in the project's root directory
        }
    }


    flavorDimensions "production"

    productFlavors {
        regular {
            buildConfigField "boolean", "MOCK", "false"
            dimension "production"
        }
        // Mock location and date data
        mock {
            buildConfigField "boolean", "MOCK", "true"
            dimension "production"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            debuggable false
        }
        unsigned {
            minifyEnabled false
            signingConfig null
            debuggable false
        }
    }

    compileOptions {
        // Target Java 21 bytecode
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    namespace 'com.gaiagps.iburn'
}

// Read Signing key from gradle.properties located in project root
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

def PLAY_SERVICES_VERSION = "17.0.0"

dependencies {

    // Android Support libraries
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.1.0'
    implementation "androidx.work:work-runtime:2.10.2"

    // Google Play Services
    implementation "com.google.android.gms:play-services-gcm:$PLAY_SERVICES_VERSION"
    implementation "com.google.android.gms:play-services-location:21.3.0"

    // 3rd Party libraries
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.patloew.colocation:colocation:1.1.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.7.3"
    implementation 'com.tonicartos:superslim:0.4.13'
    implementation 'xyz.danoz:recyclerviewfastscroller:0.1.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.media:media:1.7.0'
    implementation 'com.eclipsesource.j2v8:j2v8:6.2.1@aar'
    implementation 'org.maplibre.gl:android-sdk:11.11.0'
    implementation 'org.maplibre.gl:android-plugin-annotation-v9:3.0.2'

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.github.permissions-dispatcher:permissionsdispatcher:4.9.2'
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2"

    implementation 'com.google.firebase:firebase-crashlytics:19.4.4'
    implementation 'com.google.firebase:firebase-analytics:22.5.0'

    implementation 'androidx.room:room-rxjava2:2.7.2'
    implementation 'androidx.room:room-runtime:2.7.2'
    ksp 'androidx.room:room-compiler:2.7.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"


    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.4.0'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.annotation:annotation:1.9.1'

}

task updateData {
    // Copy items from an 'iBurn-Data' repo checked out into a
    // directory with that name adjacent to this repo's root 'iBurn-Android' directory.
    // Usage: `./gradlew :iBurn:updateData`

    // doLast ensures copying is only run when this task is
    // invoked by name, and not during general gradle configuration
    doLast {
        logger.info("Updating assets from IBurn-Data")
        // Reverse Geocoder
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/geocoder/"
            include 'bundle.js'
            into "$projectDir/src/main/assets/js/"
        }

        // Mapbox mbtiles
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/Map/"
            include 'map.mbtiles'
            into "$projectDir/src/main/assets/map/"
        }

        // Map styles
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/Map/styles/"
            include 'iburn-light.json'
            include 'iburn-dark.json'
            into "$projectDir/src/main/assets/map/"

            // Adjust asset path for Android
            filter { line ->
                line.replaceAll('asset://Map', 'asset://map')
            }
            // Remove fontstack path variable glyph definition.
            // Workaround for user POI icons (sprites?) not displaying.
            // The cause here is not yet understood. If this is resolved, you can
            // also preserve the fontstack path in the Map glyphs copy step that follows.
            filter { line ->
                line.replaceAll('asset://map/glyphs/\\{fontstack\\}/\\{range\\}.pbf', 'asset://map/glyphs/\\{range\\}.pbf')
            }
            // Filter out map style templating. Mapbox provides an API
            // for attaching a source to styles, which I think is less fragile.
            filter { line ->
                !line.contains('"composite": {"url": "mbtiles://{{mbtiles_path}}", "type": "vector"') ? line : null
            }
        }

        // Map glyphs
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/Map/glyphs/Open Sans Regular/"
            include '**/*.pbf'
            into "$projectDir/src/main/assets/map/glyphs"
        }

        // Art images
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/MediaFiles/"
            include '*.jpg'
            include '*.webp'
            into "$projectDir/src/main/assets/art_images/"
        }

        // Art audio tour
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/MediaFiles/"
            include '*.m4a'
            into "$projectDir/src/main/assets/audio_tour/"
        }

        // Json
        copy {
            from "$projectDir/../../iBurn-Data/data/$versionYear/APIData/"
            include 'camp.json'
            include 'art.json'
            include 'event.json'
            into "$projectDir/src/main/assets/json/"
        }
    }
}

tasks.register('bootstrapDatabase') {
    // Install the debug build, trigger DB bootstrap, and pull the resulting file.
    dependsOn(tasks.named('installRegularDebug'))
    doLast {
        def pkg = "com.iburnapp.iburn3.debug"
        def databaseName = project.ext.databaseName
        def bootstrapName = "bootstrap"

        // Ensure the app is fully installed
        exec {
            commandLine 'adb', 'shell', 'sleep', '1'
        }

        exec {
            commandLine 'adb', 'shell', 'am', 'broadcast', '-a',
                    'com.gaiagps.iburn.BOOTSTRAP_DB', '--es',
                    'com.gaiagps.iburn.EXTRA_DB_NAME', bootstrapName,
                    '-n', "${pkg}/com.gaiagps.iburn.DatabaseBootstrapReceiver"
        }

        def outFile = file("$projectDir/src/main/assets/databases/${databaseName}")
        outFile.parentFile.mkdirs()
        exec {
            commandLine 'adb', 'exec-out', 'run-as', pkg,
                    'cat', "/data/data/${pkg}/databases/${bootstrapName}"
            standardOutput = new FileOutputStream(outFile)
        }
    }
}

// Ensure generated source is available before compiling
tasks.named('preBuild') {
    dependsOn(generateEventYear)
}
