buildscript {
    repositories {
        jcenter()
    }
    dependencies {
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    jcenter()
    // For Mapbox SDK snapshots. Currently using unreleased map camera bounding
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 28
        versionName "2017.0.1.2"

        applicationId "com.iburnapp.iburn"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "iburn-$versionName-$versionCode"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        // J2V8 lacks arm 64 libraries, so we must limit the whole project
        // currently because other libraries like mapbox include them. https://github.com/eclipsesource/J2V8/issues/174
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        release {
            // We'll read release signingConfig's data from
            // gradle.properties in the project's root directory
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    flavorDimensions "production"

    productFlavors {
        regular {
            buildConfigField "boolean", "MOCK", "false"
            dimension "production"
        }
        // Mock location and date data
        mock {
            buildConfigField "boolean", "MOCK", "true"
            dimension "production"
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            debuggable false
        }
        unsigned {
            minifyEnabled false
            signingConfig null
            debuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

// Read Signing key from gradle.properties located in project root
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

def SUPPORT_LIB_VERSION = "26.0.1"
def PLAY_SERVICES_VERSION = "11.2.0"

dependencies {

    // Android Support libraries
    compile "com.android.support:design:$SUPPORT_LIB_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_LIB_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIB_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIB_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIB_VERSION"
    compile "com.android.support:support-dynamic-animation:$SUPPORT_LIB_VERSION"

    // Google Play Services
//    compile "com.google.android.gms:play-services-gcm:$PLAY_SERVICES_VERSION"
//    compile "com.google.android.gms:play-services-location:$PLAY_SERVICES_VERSION"

    // 3rd Party libraries
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'net.hockeyapp.android:HockeySDK:4.1.2'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.reactivex.rxjava2:rxkotlin:2.0.3'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.patloew.rxlocation:rxlocation:1.0.3'
    compile 'com.tonicartos:superslim:0.4.13'
    compile 'xyz.danoz:recyclerviewfastscroller:0.1.3'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.eclipsesource.j2v8:j2v8:4.7.1@aar'
    compile('com.mapbox.mapboxsdk:mapbox-android-sdk:5.1.3@aar') {
        transitive = true
    }
    compile 'com.roughike:bottom-bar:2.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'

    compile 'com.jakewharton:butterknife:8.6.0'
    kapt 'com.jakewharton:butterknife-compiler:8.6.0'

    compile "android.arch.persistence.room:rxjava2:1.0.0-alpha9"
    compile "android.arch.persistence.room:runtime:1.0.0-alpha9"
    kapt "android.arch.persistence.room:compiler:1.0.0-alpha9"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Local modules
    implementation project (':galactic-jungle')

    // Test dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support.test:runner:1.0.0'
    androidTestCompile 'com.android.support.test:rules:1.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0'
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIB_VERSION"

}