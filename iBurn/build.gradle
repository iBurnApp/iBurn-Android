buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 9
        versionName "2015.1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        release {
            // We'll read release signingConfig's data from
            // gradle.properties in the project's root directory
        }
        unsigned{
            storePassword = ""
            keyAlias = ""
            keyPassword = ""
        }
    }

    productFlavors {
        regular {
            buildConfigField "boolean", "MOCK", "false"
        }
        // Mock location and date data
        mock {
            buildConfigField "boolean", "MOCK", "true"
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            debuggable false
        }
        unsigned {
            signingConfig signingConfigs.unsigned
            debuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

// Read Signing key from gradle.properties located in project root
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

dependencies {

    // Android Support libraries
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'

    // Google Play Services
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
    compile 'com.google.android.gms:play-services-maps:7.5.0'
    compile 'com.google.android.gms:play-services-location:7.5.0'

    // 3rd Party libraries
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.1.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'pl.charmas.android:android-reactive-location:0.6@aar'
    compile 'com.tonicartos:superslim:0.4.13'

    apt 'net.simonvt.schematic:schematic-compiler:0.6.1'
    compile 'net.simonvt.schematic:schematic:0.6.1'

    releaseCompile project(path: ':libraries:android-gmaps-addons', configuration: 'release')
    debugCompile project(path: ':libraries:android-gmaps-addons', configuration: 'debug')
    unsignedCompile project(path: ':libraries:android-gmaps-addons', configuration: 'debug')

    // Test dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support:support-annotations:22.2.1'
}